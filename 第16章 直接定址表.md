1. 数据标号：数据标号标记了存储数据的单元的地址和长度，它不同于仅仅表示地址的地址标号。

   ```assembly
   assume cs:code
   code segment
   	a db 1,2,3,4,5,6,7,8 ;数据标号
   	b dw 0 ;数据标号
   start:mov si,0
   	  mov cx,8
   	s:mov al,a[si]
         mov ah,0
         add b,ax
         inc si
         loop s
         
         mov ax,4c00H
         int 21H
   code ends
   end start
   ```

   数据标号包含了对单元长度的描述，所以，在指令中，它可以代表一个段中的内存单元。比如，对于程序中的`b dw 0`:

   指令：`mov ax,b`

   相当于：`mov ax,cs:[8]`

   指令：`mov b,2`

   相当于：`mov word ptr cs:[8],2`

   指令：`inc b`

   相当于：`inc word ptr cs:[8]`

   在这些指令中，标号b代表了一个内存单元，地址为`code:8`，长度2个字节。

2. 需要注意的是，地址标号，只能在代码段中使用，不能在其他段中使用。而数据标识则可以在各种段中使用。

3. 下列程序将data段中a标号处的8个数据累加，结果存储在b标号处的字中。

   ```assembly
   assume cs:code,ds:data
   
   data segment
   	a db 1,2,3,4,5,6,7,8
   	b dw 0
   data ends
   
   code segment
   start:mov ax,data
   	  mov ds,ax
   	  
   	  mov si,0
   	  mov cx,8
   	s:mov al,a[si]
         mov ah,0
         add b,ax
         inc si
         loop s
         
         mov ax,4c00H
         int 21H
   code ends
   end start
   ```

   如果想在代码段中，直接用数据标号访问数据，则需要伪指令assume将标号所在的段和一个段寄存器联系起来。否则编译器在编译的时候，无法确定标号的段地址在哪个寄存器中。

4. 直接定址表：就是在两个数据集合之间建立一种映射关系，使可以用查表的方式根据给出的数据得到其在另一个集合中的对应数据。

   比如计算`sin(x),x属于{0，30，60，90，120，150，180}`。

   sin(0) = 0 
   sin(30) = 0.5 
   sin(60) = 0.866 
   sin(90) = 1 
   sin(120) = 0.866 
   sin(150) = 0.5 
   sin(180) = 0

   ```assembly
   showsin:jmp short show
           table dw ag0,ag30,ag60,ag90,ag120,ag150,ag180
           ag0   db '0',0 ;后面的0代表字符串结束标志，类似于c语言\0
           ag30  db '0.5',0
           ag60  db '0.866',0
           ag90  db '1',0
           ag120 db '0.866',0
           ag150 db '0.5',0
           ag180 db '0',0
      show:push bx
           push es
           push si
           mov bx,0b800H
           mov es,bx
           ;以下用角度值/30作为相对于table的偏移，取得对应的字符串的偏移地址，放在bx中
           mov ah,0
           mov bl,30
           div bl
           mov bl,al
           mov bh,0
           add bx,bx
           mov bx,table[bx]
           ;以下显式sin(x)对应的字符串
           mov si,160*12+40*2
     shows:mov ah,cs:[bx]
           cmp ah,0
           je showret
           mov es:[si],ah
           inc bx
           add si,2
           jmp short shows
   showret:pop si
           pop es
           pop bx
           ret
   ```

   

   

















